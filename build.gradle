plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.18'
}

group 'lekanich'
version '1.0.1'

println "CI: ${System.getenv('CI')}"

// specified the last version
def ideaVersion = '2019.3.4'
if (project.hasProperty("intellijIdeaVersion")) {
    ideaVersion = intellijIdeaVersion ? intellijIdeaVersion : defaultIdeaVersion
}
println "IdeaVersion is : ${ideaVersion}"

def artifactVersion = ideaVersion ? version + "-" + ideaVersion : version
println "ArtifactVersion is : ${artifactVersion}"

version artifactVersion

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

repositories {
    mavenCentral()
}
dependencies {
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    testCompile group: 'junit', name: 'junit', version: '4.13'
// https://mvnrepository.com/artifact/org.hamcrest/hamcrest
    testCompile group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version ideaVersion
    println "Building for version: ${version}"
}

buildPlugin {
    getArchiveClassifier().set("${intellij.version}")
}

if (project.hasProperty("intellijPublishToken")) {
    publishPlugin {
        token intellijPublishToken
    }
}

patchPluginXml {
    sinceBuild '2016.2'
    untilBuild '2019.3.4'
    changeNotes """
        Coin flipper. You can toss a coin even inside your IDE now. It's XXI century =)
    """
}

runIde {
//  jvmArgs '-Didea.ProcessCanceledException=disabled'
}