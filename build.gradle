plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.6.4'
}

group 'lekanich'
// the last digits it's the version of plugin in scope of IDEA version
String pluginVersion = '2020.2.03'

println "CI: ${System.getenv('CI')}"

// specified the last version
def ideaVersion = '2020.2'
if (project.hasProperty("intellijIdeaVersion")) {
    ideaVersion = project.property("intellijIdeaVersion")
}
println "IdeaVersion is : ${ideaVersion}"

String artifactVersion = ideaVersion ? pluginVersion + "-" + ideaVersion : pluginVersion
println "ArtifactVersion is : ${artifactVersion}"

version artifactVersion

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

repositories {
    mavenCentral()
}
dependencies {
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'

    testCompile group: 'junit', name: 'junit', version: '4.13.1'
// https://mvnrepository.com/artifact/org.hamcrest/hamcrest
    testCompile group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version ideaVersion
    println "Building for version: ${version}"
}

if (project.hasProperty("intellijPublishToken")) {
    publishPlugin {
        token intellijPublishToken
    }
}

patchPluginXml {
//    version pluginVersion
    sinceBuild '163'
    untilBuild '202.*'
    changeNotes """
        Coin flipper. You can toss a coin even inside your IDE now. It's XXI century =) <br/>
        <br/>
        [2020.2.02]<br/>
            * updated plugin icon<br/
            * change internal resolve of messages <br/
            * funny subtitles <br/
        [2020.2.01]<br/>
            * updated internal dependencies<br/
    """
}

runIde {
//  jvmArgs '-Didea.ProcessCanceledException=disabled'
}

//https://data.services.jetbrains.com/products?fields=code,name,releases.downloads,releases.version,releases.build,releases.type&code=DG,PS,DL,MPS,MPSIIP,RDCPPP,WS,AC,IIC,IIE,GO,CL,PCD,PCC,RD,PCE,IIU,RM,PCP
runPluginVerifier {
    ideVersions = "2016.3.8,2017.3.7,2018.3.6,2019.3.5,2020.1.4, 2020.2.3"
}